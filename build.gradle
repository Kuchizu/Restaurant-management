plugins {
    id 'org.springframework.boot' version '3.3.5' apply false
    id 'io.spring.dependency-management' version '1.1.6' apply false
}

ext {
    springCloudVersion = '2023.0.3'
    springdocVersion = '2.6.0'
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'jacoco'

    group = 'ru.ifmo.se.restaurant'
    version = '1.0.0'

    tasks.withType(JavaExec) {
        jvmArgs = [
            '-Xmx512m',
            '-Xms128m',
            '-XX:MaxMetaspaceSize=256m',
            '-XX:+UseG1GC',
            '-XX:+UseStringDeduplication',
            '-XX:MaxGCPauseMillis=100',
            '-XX:+UseCompressedOops'
        ]
    }

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(21)
        }
    }

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    repositories {
        mavenCentral()
    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
        }
    }

    dependencies {
        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'

        testImplementation 'org.springframework.boot:spring-boot-starter-test'
        testImplementation 'org.testcontainers:postgresql:1.19.3'
        testImplementation 'org.testcontainers:junit-jupiter:1.19.3'
    }

    jacoco {
        toolVersion = '0.8.11'
    }

    tasks.withType(Test) {
        useJUnitPlatform()
        finalizedBy jacocoTestReport
    }

    jacocoTestReport {
        dependsOn test
        mustRunAfter classes

        reports {
            xml.required = true
            html.required = true
            csv.required = false
        }

        afterEvaluate {
            classDirectories.setFrom(files(classDirectories.files.collect {
                fileTree(dir: it, exclude: [
                    '**/dto/**',
                    '**/entity/**',
                    '**/config/**',
                    '**/controller/**',
                    '**/exception/**/*Exception.class',
                    '**/*Application.class',
                    '**/client/*Fallback.class',
                    '**/client/**'
                ])
            }))
        }
    }

    jacocoTestCoverageVerification {
        enabled = false  // Disabled for Docker builds
        dependsOn jacocoTestReport

        violationRules {
            rule {
                enabled = false
                limit {
                    minimum = 0.50
                }
            }

            rule {
                enabled = false
                element = 'CLASS'
                limit {
                    counter = 'LINE'
                    value = 'COVEREDRATIO'
                    minimum = 0.30
                }
                excludes = [
                    '*.dto.*',
                    '*.entity.*',
                    '*.config.*',
                    '*.controller.*',
                    '*.*Exception',
                    '*Application',
                    '*.client.*'
                ]
            }

            rule {
                enabled = false
                element = 'PACKAGE'
                limit {
                    counter = 'LINE'
                    value = 'COVEREDRATIO'
                    minimum = 0.50
                }
                includes = [
                    '*.service.*',
                    '*.dataaccess.*'
                ]
            }
        }
    }

    check {
        // Coverage verification disabled for faster builds
        // dependsOn jacocoTestCoverageVerification
    }

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }
}

// Aggregate JaCoCo Report for all microservices
task jacocoAggregateReport(type: JacocoReport) {
    dependsOn subprojects*.test

    executionData.setFrom(fileTree(dir: '.', includes: ['**/build/jacoco/test.exec']))

    subprojects.each { subproject ->
        sourceSets subproject.sourceSets.main
    }

    reports {
        xml.required = true
        html.required = true
        html.destination = file("${buildDir}/reports/jacoco/aggregate")
    }
}

// Tests re-enabled for kitchen-service and menu-service
